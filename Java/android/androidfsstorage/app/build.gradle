plugins {
    id "com.moowork.node" version "1.3.1"
}
apply plugin: 'com.android.application'
apply plugin: 'ru.tinkoff.gradle.jarjar'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.ithit.webdav.samples.androidfsstorage"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.mergeAssets.doLast {
            delete(fileTree(dir: variant.mergeAssets.outputDir, includes: ['**/webdav.client/Help/**', '**/webdav.client/Tests/**']))
        }
    }
}

jarJar {
    rules = [
            'stax-api-1.0.1.jar'        : 'javax.xml.** com.ithit.webdav.xml.@1',
            'webdav-server-6.3.9510-Beta.jar': 'javax.xml.stream.** com.ithit.webdav.xml.stream.@1'
    ]
}

node {
    // Version of node to use.
    version = '16.14.2'

    // Version of npm to use.
    npmVersion = '8.7.0'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/node")

    // Set the work directory for NPM
    npmWorkDir = file("${project.buildDir}/npm")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/src/main/assets/wwwroot/js")
}

build.dependsOn(npmInstall)

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: 'build/libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'io.apisense:rhino-android:1.0'
    implementation 'commons-io:commons-io:2.7'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation('com.ithit.webdav.integration:android-integration:6.3.9510-Beta', {
        exclude group: 'org.nanohttpd', module: 'nanohttpd'
    })
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    jarJar 'stax:stax-api:1.0.1'
    jarJar 'com.ithit.webdav:webdav-server:6.3.9510-Beta'
    testImplementation 'junit:junit:4.12'
}
